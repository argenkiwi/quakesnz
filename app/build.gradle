apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "nz.co.codebros.quakesnz"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 23
        versionName "2.6.5"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("codebros.jks")
            keyAlias "quakesnz"

            def props = new Properties()
            project.rootProject.file("local.properties").withInputStream { props.load(it) }
            storePassword props.getProperty('key.store.password')
            keyPassword props.getProperty('key.alias.password')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    viewBinding {
        enabled = true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':core')
    implementation Dependencies.kotlinStdlibJdk7
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'

    def versionsLifecycle = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$versionsLifecycle"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$versionsLifecycle"

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation Dependencies.rxjava
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.argenkiwi:rxmodel:1.1.0'
    implementation Dependencies.dagger
    kapt Dependencies.daggerCompiler
    implementation "com.google.dagger:dagger-android:$Versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$Versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$Versions.dagger"
    implementation 'com.github.argenkiwi:dagger-viewmodel:1.2.1'
    implementation 'com.google.firebase:firebase-core:17.4.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

repositories {
    mavenCentral()
    google()
}
