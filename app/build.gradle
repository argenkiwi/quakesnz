apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "nz.co.codebros.quakesnz"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 21
        versionName "2.6.3"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("codebros.jks")
            keyAlias "quakesnz"
            File propsFile = file("secure.properties")
            if (propsFile.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propsFile))
                storePassword props.getProperty('key.store.password')
                keyPassword props.getProperty('key.alias.password')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

androidExtensions {
    experimental = true
}

ext {
    lifecycle_version = '2.0.0'
}

dependencies {
    // Modules
    implementation project(':core')
    // Support
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"
    // Material
    implementation 'com.google.android.material:material:1.0.0'
    // Play Services
    implementation 'com.google.android.gms:play-services-analytics:16.0.6'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    // RxKotlin
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    // RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // RxModel
    implementation 'com.github.argenkiwi:rxmodel:1.1.0'
    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    // Dagger Android
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    // Dagger ViewModel
    implementation 'com.github.argenkiwi:dagger-viewmodel:1.1.0'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

repositories {
    mavenCentral()
    google()
}
