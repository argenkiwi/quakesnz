apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "nz.co.codebros.quakesnz"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 21
        versionName "2.6.3"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("codebros.jks")
            keyAlias "quakesnz"
            File propsFile = file("secure.properties");
            if (propsFile.exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(propsFile))
                storePassword props.getProperty('key.store.password')
                keyPassword props.getProperty('key.alias.password')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

ext {
    playServicesVersion = '11.8.0'
    supportVersion = '27.1.0'
}

dependencies {
    // Modules
    implementation project(':core')
    // Support
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"
    // Play Services
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    // RxJava
    implementation Libraries.rxjava
    // RxKotlin
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    // RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // RxModel
    implementation 'com.github.argenkiwi:rxmodel:1.0'
    // Dagger
    implementation Libraries.dagger
    kapt Libraries.dagger_compiler
    // Dagger Android
    implementation Libraries.dagger_android
    implementation Libraries.dagger_android_support
    kapt Libraries.dagger_android_processor
    // Architecture Components
    implementation Libraries.lifecycle_extensions
    implementation Libraries.lifecycle_reactivestreams
    // Dagger ViewModel
    implementation 'com.github.argenkiwi:dagger-viewmodel:1.0.7'
    // Kotlin
    implementation Libraries.kotlin
    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

repositories {
    mavenCentral()
    google()
}