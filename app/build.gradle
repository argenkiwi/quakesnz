plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "nz.co.codebros.quakesnz"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 27
        versionName "2.7.2"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("codebros.jks")
            keyAlias "quakesnz"

            def props = new Properties()
            project.rootProject.file("local.properties").withInputStream { props.load(it) }
            storePassword props.getProperty('key.store.password')
            keyPassword props.getProperty('key.alias.password')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/rxjava.properties']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':core')
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'

    def activity_version = "1.4.0"
    implementation "androidx.activity:activity-ktx:$activity_version"

    def fragment_version = '1.4.1'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    def lifecycle_version = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-reactivestreams-ktx:2.4.1'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation Dependencies.rxjava
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.argenkiwi:rxmodel:1.1.0'
    implementation Dependencies.dagger
    kapt Dependencies.daggerCompiler
    implementation 'com.google.dagger:dagger-android:2.35.1'
    implementation 'com.google.dagger:dagger-android-support:2.35.1'
    kapt 'com.google.dagger:dagger-android-processor:2.35.1'
    implementation 'com.github.argenkiwi:dagger-viewmodel:1.2.1'
    implementation 'com.google.firebase:firebase-core:20.1.2'

    implementation Dependencies.store
    implementation Dependencies.coroutines

    implementation "com.google.dagger:hilt-android:2.41"
    kapt "com.google.dagger:hilt-compiler:2.41"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.2.0'
}

repositories {
    mavenCentral()
    google()
}
