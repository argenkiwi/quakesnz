apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "nz.co.codebros.quakesnz"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 15
        versionName "2.5.0"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("codebros.jks")
            keyAlias "quakesnz"
            File propsFile = file("secure.properties");
            if (propsFile.exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(propsFile))
                storePassword props.getProperty('key.store.password')
                keyPassword props.getProperty('key.alias.password')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {

    // Modules
    compile project(':core')

    // Support
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    // Play Services
    compile 'com.google.android.gms:play-services-analytics:11.0.2'
    compile 'com.google.android.gms:play-services-maps:11.0.2'

    // Dagger
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    // Dagger Android
    compile 'com.google.dagger:dagger-android:2.11'
    compile 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.11'

    // RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    // RxAndroid
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

repositories {
    mavenCentral()
}